[lsm_analyse_sources]
search = | tstats count as events\
where index=* earliest=-30d@h latest=-`lsm_span`@h NOT sourcetype IN(`lsm_excluded_sourcetypes`)\
by host,source,_time span=`lsm_span`\
| eval dow = lower(strftime(_time, "%A"))\
| where NOT dow IN("saturday", "sunday")\
| lookup update=t lsm_sources host source \
| where enabled="true"\
| stats avg(events) as avg_events stdev(events) as stdev_events by host,source,maxAge,maxStdDev\
| join type=left host,source\
[\
| tstats count as events\
where index=* earliest=-`lsm_span` NOT sourcetype IN(`lsm_excluded_sourcetypes`)\
by host,source\
| stats sum(events) as events by host,source\
]\
| join type=left host,source\
[\
| tstats latest(_time) as lastTime where index=* earliest=-30d@h latest=now NOT sourcetype IN(`lsm_excluded_sourcetypes`) by host,source\
]\
| eval latestTime = relative_time(now(), printf("-%s", maxAge))\
| fillnull value=0 events\
| eval diff=abs(avg_events-events)\
| eval prc=round(abs(100-(events/avg_events*100)),2)\
| eval status = case(lastTime < latestTime, "outage", diff > maxStdDev*stdev_events AND avg_events > events, "underload", diff > maxStdDev*stdev_events AND avg_events < events, "overload", 1==1, "ok")\
| eval _time = lastTime\
| reltime

[lsm_invalid_sources]
search = | savedsearch lsm_analyse_sources\ 
| where status != "ok"

[lsm_valid_sources]
search = | savedsearch lsm_analyse_sources\ 
| where status == "ok"

[lsm_generate_sources_lookup]
alert.track = 0
cron_schedule = 30 * * * *
enableSched = 1
schedule_window = 15
search = | inputlookup lsm_sources | table host, source, enabled, maxAge, maxStdDev | append [| tstats count as events\
where index=* earliest=1 NOT sourcetype IN(`lsm_excluded_sourcetypes`)\
by host,source | table host, source, enabled, maxAge, maxStdDev] | dedup host,source\
| eval enabled=coalesce(enabled,"true") | eval maxAge=coalesce(maxAge,"2h") | eval maxStdDev=coalesce(maxStdDev,1)\
| outputlookup lsm_sources
